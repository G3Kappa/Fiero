:- module(prefabs_test, []).
:- use_module(random).

% SPAWN ---------------------------------------------------------------------------------------

prefab { name: 'spawn', size: p(5, 5), tags: {spawn}, layer: 0, group: base } #-> ([
	R,R,R,R,R,
	R,R,R,R,R,
	R,R,O,R,R,
	R,R,R,R,R,
	R,R,R,R,R
]) :- 
	R = [draw_point(room)],
	O = [draw_point(room), place_marker(spawn_point)].
	
prefab { name: 'spawn', size: p(5, 5), tags: {spawn}, layer: 10, group: layout } #-> ([
	S,W,D,W,S,
	W,_,_,_,W,
	D,_,_,_,D,
	W,_,_,_,W,
	S,W,D,W,S
]) :- 
	choose((
		S = [place_marker(entity{type: feature_statue})],
		W = [draw_point(wall)],
		D = [place_marker(entity{type: feature_door})]
	),(
		W = [place_marker(entity{type: feature_statue})],
		D = []
	),(
		W = [draw_point(wall)],
		D = [place_marker(entity{type: feature_statue})],
		S = []
	)).
	
prefab { name: 'spawn', size: p(5, 5), tags: {spawn}, layer: 10, group: layout } #-> ([
	W,W,D,W,W,
	W,S,_,S,W,
	D,_,_,_,D,
	W,S,_,S,W,
	W,W,D,W,W
]) :- 
	W = [draw_point(wall)],
	choose(S = [place_marker(entity{type: feature_statue})],()),
	choose(D = [place_marker(entity{type: feature_door})],()).

% ROOMS ---------------------------------------------------------------------------------------
	
prefab { name: 'box_3x3', size: p(3, 3) } #-> ([
	W,W,W,
	W,R,W,
	W,D,W
]) :- 
	R = [draw_point(room)],
	W = [draw_point(wall)],
	D = [draw_point(room), place_marker(entity{ type: feature_door })].

prefab { name: 'box_3x3', size: p(1, 1), offset: p(1,1), layer: 1, group: contents } #-> ([X]) :- 
	member(Pool, [popcorn, item]),
	X = [place_marker(entity{ type: Pool, random_type: true })].

		
prefab { name: 'dev_grapple', size: p(5, 7) } #-> ([
	W,W,W,W,W,
	W,R,R,R,W,
	W,R,R,R,W,
	W,H,H,H,W,
	W,H,H,H,W,
	R,R,R,R,R,
	R,R,R,R,R
], [
	_,_,_,_,_,
	_,S,C,S,_,
	_,M,_,M,_,
	_,_,_,_,_,
	_,_,_,_,_,
	_,_,G,_,_,
	_,_,_,_,_
]) :- 
	R = [draw_point(room)],
	W = [draw_point(wall)],
	H = [draw_point(water)],
	S = [place_marker(entity{ type: feature_statue })],
	M = [place_marker(entity{ type: popcorn, random_type: true })],
	C = [place_marker(entity{ type: feature_chest })],
	G = [place_marker(entity{ type: projectile_grapple })].

	
prefab { name: 'dev_barrel', size: p(5, 7) } #-> ([
	W,W,W,W,W,
	W,R,R,R,W,
	W,R,R,R,W,
	W,R,R,R,W,
	W,H,H,H,W,
	R,R,R,R,R,
	R,R,R,R,R
], [
	_,_,_,_,_,
	_,M,M,M,_,
	_,M,B,M,_,
	_,M,S,M,_,
	_,_,_,_,_,
	_,_,G,_,_,
	_,_,_,_,_
]) :- 
	R = [draw_point(room)],
	W = [draw_point(wall)],
	H = [draw_point(water)],
	S = [place_marker(entity{ type: feature_statue })],
	M = [place_marker(entity{ type: popcorn, random_type: true })],
	B = [place_marker(entity{ type: dummy_explosive_barrel })],
	G = [place_marker(entity{ type: projectile_rock, args: _{charges: 99} })].